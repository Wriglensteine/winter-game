link=blueDeck.csv,name,resource1,resource2,resource3,penalty,resource4,resource5,resource6
macro=resource,(resourceValue),(cardNum),(x),(y),(xwidth),(ywidth)
    if = (resourceValue) = "WOOD"
        image=(cardNum),image_wood.png, (x),(y),(xwidth),(ywidth)
    elseif = (resourceValue) = "METAL"
        image=(cardNum),image_metal.png, (x),(y),(xwidth),(ywidth)
    elseif = (resourceValue) = "FURS"
        image=(cardNum),image_furs.png, (x),(y),(xwidth),(ywidth)
    elseif = (resourceValue) = "FOOD"
        image=(cardNum),image_food.png, (x),(y),(xwidth),(ywidth)
    endif
end
macro=penalty,(resourceValue),(cardNum),(x),(y),(xwidth),(ywidth)
    if = (resourceValue) = "LOST"
        image=(cardNum),token_lost.png, (x),(y),(xwidth),(ywidth)
    elseif = (resourceValue) = "BLOOD"
        image=(cardNum),image_blood.png, (x),(y),(xwidth),(ywidth)
    elseif = (resourceValue) = "TWOBLOOD"
        image=(cardNum),image_twoblood.png, (x),(y),(xwidth),(ywidth)
    elseif = (resourceValue) = "ANY"
        image=(cardNum),image_any.png, (x),(y),(xwidth),(ywidth)
    elseif = (resourceValue) = "FURFOOD"
        image=(cardNum),image_furfood.png, (x),(y),(xwidth),(ywidth)
    endif
end
dpi=300
oversample=2
[all]=1-40
[allbacks]=41-80
[darkest]=#182d41
[darker]=#20486c
[dark]=#2c577d
[light]=#487297
[lighter]=#8facc7
[darkgrey]=#494949
[grey]=#636363
[lightgrey]=#8a8a8a
[textcolor]=#757575
PAGE=21,29.7,PORTRAIT,HV,#FFFFFF,41-80

; card backgrounds
border=rounded, [darkest], 0.5
image=[all],blue_card_base.png,0,0,100%,100%

; text
roundrect=[all],0.8,1,0.1,4, [lighter],[lighter],1,3,3
font= "arial", 13, TB, [textcolor]
text = [all], [name],-0.05,0.8,1.7,4.5,center,center,-90

; returned penalties
penalty=[penalty],[all],2,6.4,2,2

; resource rewards bars
if = _NOT_ ([resource1] = "")
    roundrect=[all],2.0,5,3,0.1,[dark],[dark],1,3,3
endif
if = _NOT_ ([resource4] = "")
    roundrect=[all],1.3,6.2,3,0.1,[dark],[dark],1,3,3
endif

; rewarded resources
resource=[resource1],[all],1.75,4.7,0.7,0.7
resource=[resource2],[all],2.5,4.7,0.7,0.7
resource=[resource3],[all],3.25,4.7,0.7,0.7
resource=[resource4],[all],1.05,5.9,0.7,0.7
resource=[resource5],[all],1.8,5.9,0.7,0.7
resource=[resource6],[all],2.55,5.9,0.7,0.7

; back of cards and returns
rectangle=[allbacks],0,0,100%,100%,[darkgrey]
POLYGON = [allbacks], 0, 0.5, 6, 6, 4, 0, [grey], [grey], 0.1
image=[allbacks], image_E.png, 0.5, 1, 5, 5
duplex=1-40,41-80
Print=duplex
display=./cards_blue_output_fronts.png,1,40
display=./cards_blue_output_backs.png,41,80